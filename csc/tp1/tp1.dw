//Interface de communication avec le centre de controle
//var:from indique la source (1..3 pour floor, 4 pour la cage)
//var:say indique le message 
// (Pour floor : 1 = surcourse bas / 2 = stop / 3 = surcourse haut / 4 = call)
// (Pour la cage : 1..3 = call / 4 = capteur cage -> ouvrir la porte)
interface SignalReceiveIt(int from,int say){
    handle[from][say];
}

//Interface de controle du moteur
interface MotorControlIt{
    ascent;
    stop;
    descent;
}

//Interface de controle de la porte
interface DoorControlIt{
    open;
    close;
}

// Description du centre de controle
component CONTROLCENTER(int N) {
    require
        door:DoorControlIt;
        motor:MotorControlIt;
    provide
        cc:SignalReceiveIt(1,2);
    /%
        const SCB = 1 //Surcourse bas
        const STOP = 2 //Stop
        const SCH = 3 //Surcourse haut

        const Cage = 4 //La cage
        const Blocked = 4 //Signal de porte bloqué
        range All = 1..4 //Range complete
        range Floors = 1..3 //Range des floors
        const Max = 3 //Floor max
        const Call = 4 //Message d'appel

        CONTROLCENTER(N=1) = CTRL[1][1],
        //On interprete tout les messages
        //TRES IMPORTANT :
        //Lorsque la source est indiqué, elle n'est que le contenu du message. C'est pourquoi le floor1 peut
        //théoriquement parfaitement envoyer un message en se faisant passer pour le floor 3
        //Cela explique la multiplication de l'alphabet lors du run
        CTRL[from:1..N][to:1..N] =
        (
            //Lorqu'un bouton est appuyé ...
            cc.handle[i:Floors][Call] ->
            (when(from == to & from == i) door.open -> CTRL[from][to]
            |when(from == to & from > i) door.close -> motor.descent -> CTRL[from][i]
            |when(from == to & from < i) door.close -> motor.ascent -> CTRL[from][i]
            |when(from != to) ignore -> CTRL[from][to]
            )
            //Lorsqu'un bouton de la cage est appuyé ...
            |cc.handle[Cage][i:Floors] ->
            (when (from != to) ignore -> CTRL[from][to]
            |when(from == to & from == i) door.open -> CTRL[from][to]
            |when(from == to & from > i) door.close -> motor.descent -> CTRL[from][i]
            |when(from == to & from < i) door.close -> motor.ascent -> CTRL[from][i]
            )
            //Lorsque la porte est bloqué
            |cc.handle[Cage][Blocked] ->
            (when(from == to) door.open -> CTRL[from][to]
            |when(from != to) ignore -> CTRL[from][to]
            )
            //Gestion des surcourse MAX haute et basse
            |cc.handle[1][SCB] -> CTRL[0][0]
            |cc.handle[Max][SCH] -> CTRL[0][0]
            //Arret à l'etage i ...
            |cc.handle[i:Floors][STOP] ->
            (when(from == to & i == from) ignore -> CTRL[from][to]
            |when(from == to & i != from) alert -> CTRL[0][0]
            |when(from > to & i > from | i < to ) alert -> CTRL[0][0]
            |when(from < to & i < from | i > to ) alert -> CTRL[0][0]
            |when(from > to & i <= from & i > to) ignore -> CTRL[from][to]
            |when(from < to & i >= from & i < to) ignore -> CTRL[from][to]
            |when(from != to & i == to) motor.stop -> door.open -> CTRL[to][to]
            )
            //Passage devant un capteur bas d'un floor quelquonque
            |cc.handle[i:2..Max][SCB] ->
            (when(from == to) alert -> CTRL[0][0]
            |when(from < to & i == to) motor.ascent -> CTRL[from][to]
            |when(from < to & i != to) ignore -> CTRL[from][to]
            |when(from > to) ignore -> CTRL[from][to]
            )
            //Pasage devant un capteur bas
            |cc.handle[i:1..Max-1][SCH] ->
            (when(from == to) alert -> CTRL[0][0]
            |when(from > to & i == to) motor.descent -> CTRL[from][to]
            |when(from > to & i != to) ignore -> CTRL[from][to]
            |when(from < to) ignore -> CTRL[from][to]
            )
            |when(from == 0 & to == 0) motor.stop -> CTRL[0][0]
        )+{door.DoorControlIt,motor.MotorControlIt,cc.SignalReceiveIt}.
    %/
}

//Description de la cage d'ascenseur
component ELEVATORCAGE (int NBFLOOR){
    require
        cc:SignalReceiveIt(1,2);
    provide
        door:DoorControlIt;

        /%
            ELEVATORCAGE(NBFLOOR = 1) = EC[NBFLOOR],
            EC[NBFLOOR] = (
                cc.handle[NBFLOOR+1][1..NBFLOOR+1] -> EC[NBFLOOR]
                | door.open -> EC[NBFLOOR]
                | door.close -> EC[NBFLOOR]
            )+{door.DoorControlIt,cc.SignalReceiveIt}.

        %/
}

//Description d'un etage
component FLOOR(int NUMFLOOR) {
    require
        cc:SignalReceiveIt(1,2);
        /%
            range SIGNALS = 1..4

            FLOOR(NUMFLOOR=1) = FLO[NUMFLOOR],
            FLO[i:NUMFLOOR..NUMFLOOR] = (
                cc.handle[i][SIGNALS] -> FLO[i]
            )+{cc.SignalReceiveIt}.
        %/
}

component MOTOR {
    provide
        motor:MotorControlIt;
        /%
            MOTOR = (
                motor.ascent -> motor.ascent -> motor.stop -> MOTOR
               |motor.descent -> motor.descent -> motor.stop -> MOTOR
            ).
        %/
}

component GLOBAL {
    inst
        cc:CONTROLCENTER(3);
        cage:ELEVATORCAGE(3);
        motor:MOTOR;
        floor1:FLOOR(1);
        floor2:FLOOR(2);
        floor3:FLOOR(3);
    bind
        floor1.cc -- cc.cc;
        floor2.cc -- cc.cc;
        floor3.cc -- cc.cc;
        cc.motor -- motor.motor;
        cc.door -- cage.door;
        cage.cc -- cc.cc;
}
